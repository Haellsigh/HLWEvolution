package Commands

import StringUtils
import ClosureEvents
import LinkedList

import Initialization
import IncomeValues
import IncomeBoard

let MSG_DURATION = 10.

public bool DBG_MODE = false

public string commandHelp = compiletime(
    "help: Shows the list of commands ingame\n\n" +
    "zoom x or cam x: Sets the camera zoom (between 50 and 220)\n\n"
    )

@loadinit function initChatEvent() 
    EventListener.add(EVENT_PLAYER_CHAT_FILTER) ->
        var str = GetEventPlayerChatString()
        if str.startsWith("-")
            parseCommand(str)

function parseCommand(string str)
    let p = GetTriggerPlayer()

    LinkedList<string> args = str.substring(1).split(" ")

    let cmd = args.get(0)

    switch cmd
        case "help"
            p.print("List of commands:\n\n" + commandHelp, MSG_DURATION)
        case "dbg"
            DBG_MODE =true
            print("Debug mode enabled!")
        case "income"
            let which = args.get(1)
            let value = args.get(2).toInt()
            if which != null
                switch which
                    case "g"
                        playerIncome.get(p).gold += value
                    case "l"
                        playerIncome.get(p).lumber += value
                updateIncome(p)
        case "zoom" | "cam"
            var zstr = args.get(1)
            if zstr != null
                var zoom = zstr.toInt()
                
                // Minimum zoom
                if zoom < 50
                    zoom = 50
                    p.print("Minimum zoom is 50", MSG_DURATION)
                // Maximum zoom
                else if zoom > 220
                    zoom = 220
                    p.print("Maximum zoom is 220", MSG_DURATION)

                SetCameraFieldForPlayer(p, CAMERA_FIELD_TARGET_DISTANCE, 16.5 * zoom, 0.3)

        default
            p.print("Unknown command", MSG_DURATION)